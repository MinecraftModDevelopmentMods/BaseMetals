def corePlugin = 'com.mcmoddev.lib.asm.ASMPlugin'

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'gradle'
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.10'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1'
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.3'
        classpath "com.jaredsburrows:gradle-checker-framework-plugin:0.2.0"
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'jacoco'
//apply plugin: 'com.jaredsburrows.checkerframework'

repositories {
	mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile('org.junit.jupiter:junit-jupiter-api:5.0.+')
    testCompile('org.junit.platform:junit-platform-launcher:1.0.+')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.0.+')
    testCompile('org.mockito:mockito-core:2.+')
}

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    // configure jacoco to analyze the junitPlatformTest task
    jacoco {
        // this tool version is compatible with
        toolVersion = '0.7.9'
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport",
            {
                sourceDirectories = files("./src/main")
                classDirectories = files("$buildDir/classes/main")
                executionData junitPlatformTestTask
            })
}

junitPlatform {
    platformVersion '1.0.3' // optional, defaults to plugin version
    //logManager 'org.apache.logging.log4j.jul.LogManager'
    reportsDir file('build/test-results/junit-platform') // this is the default
    // enableStandardTestTask true
    // selectors (optional)
    // filters (optional)
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs = [
        '-Xlint:deprecation', // deprecation, rawtypes, cast, unchecked, all
//		'-Xdiags:verbose',
//      '-Werror'
    ]
}

javadoc {
    failOnError = false
}

description = 'Base Metals'
def mod_file = getModFile()
def mc_version = '1.10.2'

ext {
    short_version = getVersion('VERSION', mod_file)
}

version = mc_version + '-' + short_version

if (System.getenv().BUILD_NUMBER) {
    version += '.' + System.getenv().BUILD_NUMBER
}

group = 'com.mcmoddev'
archivesBaseName = 'BaseMetals'
sourceCompatibility = targetCompatibility = '1.8'

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ''
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file 'secret.json'
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = '1.10.2-12.18.3.2185'
    runDir = 'run'
    mappings = 'stable_29'
    // coreMod = corePlugin
    clientJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
    serverJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]

    replace '@FINGERPRINT@', project.findProperty('signSHA1')
}

repositories {
    maven { // Mantle, TCon, JEI
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven/'
    }
    maven { // CCL, CCC, NEI
        name 'Chickenbones Repo'
        url 'http://chickenbones.net/maven/'
    }
    maven { // The One Probe, EnderIO, EnderCore
        name 'tterrag'
        url 'http://maven.tterrag.com/'
    }
    maven { // Tesla
        name 'MMD'
        url 'https://maven.mcmoddev.com/'
    }
    maven { // MCMultipart
        name 'amadornes'
        url 'http://maven.amadornes.com/'
    }
    maven { // CraftTweaker, ZenScript
        name 'blamejared'
        url 'http://maven.blamejared.com/'
    }
    maven { // IC2
        name 'industrialcraft'
        url 'http://maven.ic2.player.to/'
    }
    maven { // HWYLA
    	name 'tehnut'
    	url 'http://tehnut.info/maven/'
    }
    maven { // CoFH
    	name 'Covers Maven'
    	url 'http://maven.covers1624.net/'
    }
	maven {
        name = 'CurseForge'
        url = 'https://minecraft.curseforge.com/api/maven/'
    }
	maven {
        name 'opencomputers'
        url 'http://maven.cil.li/'
    }
    maven { // Mekanism, TAIGA
        name 'jitpack'
        url 'https://jitpack.io/'
    }
    maven { // Buildcraft
        name 'buildcraft'
        url 'https://mod-buildcraft.com/maven/'
    }
    maven { // Storage Drawers
        name 'thiakil'
        url 'http://maven.thiakil.com/'
    }
    ivy {
        url "http://ae-mod.info/builds"
        layout "pattern", {
            artifact "[module]-[revision](-[classifier])(.[ext])"
        }
	}
}

dependencies {
    deobfProvided ("com.github.aidancbrady:Mekanism:${mekanism_version}") {
        exclude group: 'mezz.jei'
        exclude group: 'MCMultiPart'
    }

    deobfProvided ("com.github.TeamFRM:TAIGA:${taiga_version}") {
        exclude group: 'mezz.jei'
        exclude module: 'Mantle'
        exclude module: 'TConstruct'
    }

    deobfCompile "slimeknights.mantle:Mantle:${mc_version}-${mantle_version}"

    deobfCompile "slimeknights:TConstruct:${mc_version}-${tconstruct_version}"

    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
//  deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"

    deobfCompile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal"

    deobfCompile ("codechicken:CodeChickenCore:${mc_version}-${ccc_version}:universal") {
        exclude group: 'codechicken'
    }

    // Crashes in game
    deobfProvided ("codechicken:NotEnoughItems:${mc_version}-${nei_version}:universal") {
        exclude group: 'codechicken'
        exclude group: 'mezz.jei'
    }

    deobfCompile "com.mcmoddev:AdditionalLootTables:${mc_version}-${alt_version}"
    deobfCompile "com.mcmoddev:OreSpawn:${mc_version}-${orespawn_version}"

    deobfCompile "MCMultiPart:MCMultiPart:${mcmp_version}:universal"

    deobfCompile "mcjty.theoneprobe:TheOneProbe:${top_mc_version}-${top_version}"
//  deobfProvided "mcjty.theoneprobe:TheOneProbe:${top_mc_version}-${top_version}:api"

    deobfProvided "portablejim:VeinMiner:${veinminer_version}:api"

    deobfCompile "net.industrial-craft:industrialcraft-2:${ic2_version}"
//  deobfProvided "net.industrial-craft:industrialcraft-2:${ic2_version}:api"

    deobfCompile ("MineTweaker3:MineTweaker3-MC1102-Main:${mc_version}-${minetweaker_version}") {
        exclude group: 'org.ow2.asm'
    }

	deobfCompile "com.blamejared:MTLib:${mtlib_version}"

    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
//  deobfProvided "mcp.mobius.waila:Hwyla:${hwyla_version}:api"

    deobfCompile ("cofh:CoFHCore:${mc_version}-${cofhcore_version}:universal") {
        exclude group: 'mezz.jei'
    }

    deobfProvided ("cofh:ThermalFoundation:${mc_version}-${thermal_foundation_version}:universal") {
        exclude module: 'CoFHCore'
    }

    deobfProvided ("cofh:ThermalExpansion:${mc_version}-${thermal_expansion_version}:universal") {
        exclude group: 'codechicken'
        exclude group: 'mezz.jei'
        exclude module: 'CoFHCore'
//        exclude module: 'ThermalFoundation'
    }

    deobfCompile "com.enderio.core:EnderCore:${mc_version}-${endercore_version}"
//  deobfProvided "com.enderio.core:EnderCore:${mc_version}-${endercore_version}:api"

//  deobfCompile "com.enderio:EnderIO:${mc_version}-${enderio_version}"
    deobfProvided "com.enderio:EnderIO:${mc_version}-${enderio_version}:api"

	deobfProvided "mcjty:compatlayer:1.10-${compatlayer_version}"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': short_version, 'mcversion': project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLCorePlugin': corePlugin
//      attributes 'FMLAT' : 'mmdlib_at.cfg'
    }
}

task deobfJar(type: Jar) {
    classifier = 'deobf'
    from sourceSets.main.output
}

task signJar(type: SignJar, dependsOn: reobfJar) {

    // Skips if the keyStore property is missing.
    onlyIf {
        project.hasProperty('keyStore')
    }

    // findProperty allows us to reference the property without it existing. 
    // Using project.propName would cause the script to fail validation if 
    // the property did not exist. 
    keyStore = project.findProperty('keyStore')
    alias = project.findProperty('keyStoreAlias')
    storePass = project.findProperty('keyStorePass')
    keyPass = project.findProperty('keyStoreKeyPass')
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact deobfJar {
                classifier 'deobf'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    apiKey = secret.curseforgeAPIKey
    project {
        id = '240967'
        changelog = file('CHANGELOG.txt')
        releaseType = 'release'
        addGameVersion(project.mc_version)
        def projectName = 'Base Metals'
        def displayVersion = getVersion('VERSION', mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += '.' + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "$project.description $displayVersion"
            relations {
                requiredLibrary 'mmd-orespawn'
                optionalLibrary 'additional-loot-tables'
            }
        }
        addArtifact(sourceJar) {
            displayName = "$project.description $displayVersion Sources"
        }
        addArtifact(deobfJar) {
            displayName = "$project.description $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "$project.description $displayVersion Javadoc"
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', secret.sonarHost
        property 'sonar.organization', secret.sonarOrganization
        property 'sonar.login', secret.sonarToken
        property 'sonar.projectName', project.archivesBaseName
        property 'sonar.projectKey', "$project.group:$project.archivesBaseName"
    }
}

String getModFile() {
    String path = ''
    FileTree tree = fileTree(dir: 'src/main/java')
    tree.include '**/*.java'
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim()
                if (s.startsWith('@Mod(')) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path
}

String getVersion(String type, String mod_file) {
    String major = '0'
    String revision = '0'
    String patch = '0'
    File file = file(mod_file)
	def prefix = ~/^(?:public|protected|private) static final String $type = ".*$/
    file.eachLine { String s ->
    	s = s.trim()
        if (s ==~ prefix || prefix.matcher(s).matches()) {
        	String[] bits = s.split("=")
        	String interesting = bits[1].trim()
        	interesting = interesting.substring(1, interesting.length() - 2)
            String[] pts = interesting.trim().split("\\.")

            major = pts[0] ?: '0'
            revision = pts[1] ?: '0'
            patch = pts[2] ?: '0'
        }
    }
    return "$major.$revision.$patch"
}
