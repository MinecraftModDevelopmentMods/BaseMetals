def corePlugin = "com.mcmoddev.lib.asm.ASMPlugin"

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "maven-publish"

javadoc {
    failOnError = false
}

def mod_file = getModFile()
def mc_version = "1.10.2"
def short_version = getVersion("VERSION", mod_file)
version = mc_version + "-" + short_version
if (System.getenv().BUILD_NUMBER) {
    version += "." + System.getenv().BUILD_NUMBER
}
group = "com.mcmoddev"
archivesBaseName = "BaseMetals"
sourceCompatibility = targetCompatibility = "1.8"

class Secrets {
    def data = null

    def getProperty(String key) {
        return data ? data[key] : ""
    }
}

import groovy.json.JsonSlurper

def secretFile
if (System.getenv().SECRET_FILE) {
    secretFile = file System.getenv().SECRET_FILE
} else {
    secretFile = file "secret.json"
}

project.ext.secret = new Secrets()
if (secretFile.exists()) {
    secretFile.withReader {
        project.ext.secret.data = new JsonSlurper().parse it
    }
}

minecraft {
    version = "1.10.2-12.18.3.2185"
    runDir = "run"
    mappings = "stable_29"
//  coreMod = corePlugin
    clientJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
    serverJvmArgs = ["-Dfml.coreMods.load=$corePlugin"]
}

repositories {
    maven {
        name "DVS1 Maven FS"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "Chickenbones Repo"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "tterrag"
        url "http://maven.tterrag.com/"
    }
    maven {
        name "MMD"
        url "https://maven.mcmoddev.com/"
    }
    maven {
        name "amadornes"
        url "http://maven.amadornes.com/"
    }
    maven {
        name "epoxide"
        url "http://maven.epoxide.org"
    }
    maven { // CraftTweaker
        name 'jared maven'
        url "http://blamejared.com/maven"
    }
    maven {
        name "jitpack"
        url "https://jitpack.io"
    }
    maven {
        name "industrialcraft"
        url "http://maven.ic2.player.to"
    }
    maven { // Waila is here
    	name "tehnut"
    	url "http://tehnut.info/maven"
    }
}

dependencies {
    // c510ce7d8c = https://github.com/aidancbrady/Mekanism/commit/c510ce7d8cf7810500e032e2f713796078d26c6f
    // 7e3c806a8f = https://github.com/aidancbrady/Mekanism/commit/7e3c806a8f136526129967ba1b25946d8f05ccd9
    compileOnly ("com.github.aidancbrady:Mekanism:7e3c806a8f") {
        exclude group: 'MCMultiPart'
        exclude group: 'mezz.jei'
    }

    deobfCompile ("com.github.TeamFRM:TAIGA:4db215fc65") {
        exclude group: 'MCMultiPart'
        exclude group: 'mezz.jei'
        exclude group: 'slimeknights.mantle'
        exclude group: 'slimeknights'
    }

    deobfCompile ("slimeknights:TConstruct:${mc_version}-${tconstruct_version}") {
        exclude group: 'mezz.jei'
    }
    deobfCompile ("slimeknights.mantle:Mantle:${mc_version}-${mantle_version}") {
        exclude group: 'mezz.jei'
    }
	
//  We do this to make NEI happy as there is no deobf or dev JEI jar for the deobf instance
//  deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
//  runtime "mezz.jei:jei_${mc_version}:${jei_version}:dev"

    deobfCompile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal"
    deobfCompile ("codechicken:CodeChickenCore:${mc_version}-${ccc_version}:universal") {
        exclude group: 'codechicken'
    }
    deobfCompile ("codechicken:NotEnoughItems:${mc_version}-${nei_version}:universal") {
        exclude group: 'mezz.jei'
        exclude group: 'codechicken'
    }
    deobfCompile "mmd:orespawn:${orespawn_version}-${mc_version}"
//  deobfCompile "MCMultiPart:MCMultiPart:${mcmp_version}:universal"
    deobfCompile "MCMultiPart:MCMultiPart-experimental:${mcmp_experimental_version}:universal"
//  compile "mcjty:theoneprobe:TheOneProbe:${mc_version}-${top_version}"

    deobfCompile ("net.industrial-craft:industrialcraft-2:2.6.161-ex110:api") {
        exclude group: 'mezz.jei'
    }
    deobfCompile ("MineTweaker3:MineTweaker3-API:3.+") {
        exclude group: 'mezz.jei'
    }
    deobfCompile ("mcp.mobius.waila:Hwyla:+") {
        exclude group: 'mezz.jei'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": short_version, "mcversion": project.minecraft.version
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes "FMLCorePluginContainsFMLMod": "true"
        attributes "FMLCorePlugin": corePlugin
    }
}

/*
task apiJar(type: Jar) {
    from apiClasses
    classifier = 'apiClasses'
}
*/

task deobfJar(type: Jar) {
    classifier = "deobf"
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.getDestinationDir()
}

artifacts {
//  archives apiJar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
/*
            artifact apiJar {
                classifier "api"
            }
*/
            artifact deobfJar {
                classifier "deobf"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            credentials {
                username secret.username
                password secret.password
            }
            url secret.url
        }
    }
}

curseforge {
    apiKey = secret.curseforgeAPIKey
    project {
        id = "240967"
        changelog = file("CHANGELOG.txt")
        releaseType = "release"
        addGameVersion(project.mc_version)
        def projName = "Base Metals"
        def displayVersion = getVersion("VERSION", mod_file)
        if (System.getenv().BUILD_NUMBER) {
            displayVersion += "." + System.getenv().BUILD_NUMBER
        }
        mainArtifact(jar) {
            displayName = "$projName $displayVersion"
            relations {
                requiredLibrary "mmd-orespawn"
                optionalLibrary "additional-loot-tables"
            }
        }
//        addArtifact(apiJar) {
//            displayName = "$projName $displayVersion API"
//        }
        addArtifact(sourceJar) {
            displayName = "$projName $displayVersion Sources"
        }
        addArtifact(deobfJar) {
            displayName = "$projName $displayVersion Development"
        }
        addArtifact(javadocJar) {
            displayName = "$projName $displayVersion Javadoc"
        }
    }
}

String getModFile() {
    String path = "";
    FileTree tree = fileTree(dir: "src/main/java")
    tree.include "**/*.java"
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim();
                if (s.startsWith("@Mod")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path;
}

String getVersion(String type, String mod_file) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}
