buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "com.matthewprenger:CurseGradle:1.0-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

def mod_file = getModFile()
version = getVersion("VERSION", mod_file)
group= "cyano.basemetals"
archivesBaseName = "BaseMetals_1.10.2"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
    version = "1.10.2-12.18.2.2099"
    runDir = "run"
    mappings = "stable_29"
//    coreMod = "basemetals.asm.BaseMetalPlugin"
}

repositories {
    maven {
        name "DVS1 Maven FS"
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "Chickenbones Repo"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "tterrag"
        url "http://maven.tterrag.com/"
    }
	maven {
		name "Ilexiconn"
		url "https://maven.ilexiconn.net/"
	}
	maven {
		name "amadornes"
		url "http://maven.amadornes.com/"
	}
}

dependencies {
    compile "slimeknights:TConstruct:${mc_version}-${tconstruct_version}:deobf"
    compile "slimeknights.mantle:Mantle:${mc_version}-${mantle_version}:deobf"
    compile "mezz.jei:jei_${mc_version}:${jei_version}"
    compile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    compile "codechicken:CodeChickenCore:${mc_version}-${ccc_version}:deobf"
    compile "codechicken:NotEnoughItems:${mc_version}-${nei_version}:deobf"
	compile "mmd:orespawn:${orespawn_version}-${mc_version}:dev"
//	deobfCompile "MCMultiPart:MCMultiPart:${mcmp_version}:universal"
	compile "MCMultiPart:MCMultiPart:${mcmp_version}:deobf"
//	deobfCompile "MCMultiPart:MCMultiPart-experimental:${mcmp_experimental_version}:universal"
//	compile "MCMultiPart:MCMultiPart-experimental:${mcmp_experimental_version}:deobf"
//	compile "mcjty:theoneprobe:TheOneProbe:${mc_version}-${top_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//jar {
//    manifest {
//        attributes("FMLCorePluginContainsFMLMod": "true", "FMLCorePlugin": "basemetals.asm.BaseMetalPlugin")
//    }
//}

task apiJar(type: Jar) {
    from apiClasses
    classifier = 'apiClasses'
}

task deobfJar(type: Jar) {
    classifier = "deobf"
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.getDestinationDir()
}

artifacts {
//  archives apiJar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}

curseforge {
    apiKey = ""
    project {
        id = "240967"
        changelog = file("CHANGELOG.txt")
        releaseType = "release"
        mainArtifact(jar) {
            displayName = "Base Metals $project.version"
            relations {
                requiredLibrary "mmd-orespawn"
                optionalLibrary "additional-loot-tables"
            }
        }
        addArtifact sourceJar
        addArtifact deobfJar
        addArtifact javadocJar
    }
}

String getModFile() {
    String path = "";
    FileTree tree = fileTree(dir: "src/main/java")
    tree.include '**/*.java'
    tree.visit { element ->
        if (element.file.isFile()) {
            element.file.eachLine { String s ->
                s = s.trim();
                if (s.startsWith("@Mod")) {
                    path = "src/main/java/$element.relativePath"
                }
            }
        }
    }
    return path;
}

String getVersion(String type, String mod_file) {
    String major = "0";
    String revision = "0";
    String patch = "0";
    String prefix = "public static final String $type = \"";
    File file = file(mod_file)
    file.eachLine { String s ->
        s = s.trim();
        if (s.startsWith(prefix)) {
            s = s.substring(prefix.length(), s.length() - 2);
            String[] pts = s.split("\\.");

            major = pts[0];
            revision = pts[1];
            patch = pts[2];
        }
    }
    return "$major.$revision.$patch";
}
